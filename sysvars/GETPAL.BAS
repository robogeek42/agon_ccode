10 REM Read palette example
20 MODE 0 : REM 16 colour mode

40 TW% = FN_GETBYTE_VDP(&13): REM GET SCREEN WIDTH IN CHARS.
50 TH% = FN_GETBYTE_VDP(&14): REM GET SCREEN HEIGHT IN CHARS.
60 COLS% = FN_GETBYTE_VDP(&15): REM GET NUMBER OF COLOURS.
70 GW% = FN_GETWORD_VDP(&0F): REM Width in pixels
80 GH% = FN_GETWORD_VDP(&11): REM Height in pixels

100 PRINT "MODE 0 "+STR$(TW%)+"x"+STR$(TH%)+" COLS:"+STR$(COLS%)+" ";
110 PRINT STR$(GW%)+"x"+STR$(GH%)

120 COLOUR 1: PRINT "COL 1 DARK RED"
140 VDU 19, 1, 255, &55, &AA, &FF
150 Y=0:FOR X=0 TO 2048:Y=Y+X:NEXT
160 COLOUR 1: PRINT "COL 1 (0000AA) redefined as (FFAA55)"
180 C.15
200 FOR L=0 TO 31
220 PRINT "COL "+RIGHT$("00"+STR$(L),2)+": "+RIGHT$("000000"+STR$~(FNgetPalette(L)),6)+"   IND "+STR$~(FNgetPaletteIndex(L))
240 NEXT

999 END

1000 DEF FNgetPalette(P)
1010 VDU 23, 0, &94, P
1015 Y=0:FOR X=0 TO 256:Y=Y+X:NEXT
1020 =FN_GETWWORD_VDP(&0A)

1100 DEF FNgetPaletteIndex(P)
1110 VDU 23, 0, &94, P
1115 Y=0:FOR X=0 TO 256:Y=Y+X:NEXT
1120 =FN_GETBYTE_VDP(&16)

2000 REM OSBYTE/WORD/WWORD calls
2010 DEF FN_GETBYTE_VDP(V%): A%=&A0: L%=V%: =USR(&FFF4)
2020 DEF FN_GETWORD_VDP(V%): =FN_GETBYTE_VDP(V%)+256*FN_GETBYTE_VDP(V%+1)
2030 DEF FN_GETWWORD_VDP(V%): =FN_GETBYTE_VDP(V%)+256*FN_GETBYTE_VDP(V%+1)+256*256*FN_GETBYTE_VDP(V%+2)
